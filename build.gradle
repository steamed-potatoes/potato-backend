def javaProjects = [
    project(':potato-api'),
    project(':potato-domain')
]

buildscript {
    ext {
        springBootVersion = '2.3.8.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

configure(javaProjects) {
    apply plugin: 'java-library'
    apply plugin: 'eclipse'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'jacoco'

    group = 'com.potato'
    sourceCompatibility = '11'

    repositories {
        mavenCentral()
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        implementation 'org.springframework.boot:spring-boot-starter-security'
        testImplementation 'org.springframework.security:spring-security-test'
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
        compile group: 'org.springframework.boot', name: 'spring-boot-starter-validation', version: "${springBootVersion}"
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    jacoco {
        toolVersion = '0.8.5'
        reportsDir = file("$buildDir/jacoco-report")
    }

    jacocoTestReport {
        reports {
            html.enabled true
            xml.enabled true
            csv.enabled false
        }
    }

    test {
        jacoco {
            destinationFile = file("$buildDir/jacoco/jacoco.exec")
        }
        useJUnitPlatform()
        finalizedBy 'jacocoTestReport'
    }

}
