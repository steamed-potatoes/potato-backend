buildscript {
    ext {
        springBootVersion = '2.3.8.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

allprojects {
    group = 'com.potato'
}

subprojects {
    apply {
        plugin('java-library')
        plugin('eclipse')
        plugin('org.springframework.boot')
        plugin('io.spring.dependency-management')
    }

    sourceCompatibility = '11'

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        implementation 'org.springframework.boot:spring-boot-configuration-processor'

        testImplementation('org.springframework.boot:spring-boot-starter-test')

        // Lombok
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    test {
        useJUnitPlatform()
    }
}

def testProjects = [
    project(':potato-admin'),
    project(':potato-api'),
    project(':potato-domain'),
    project(':potato-common')
]

configure(testProjects) {
    apply {
        plugin('jacoco')
    }

    jacoco {
        toolVersion = '0.8.5'
    }

    jacocoTestReport {
        executionData(fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec"))
        reports {
            html.enabled true
            xml.enabled true
            csv.enabled false
        }
        testProjects.forEach {
            sourceSets(it.sourceSets["main"] as SourceSet)
        }
    }

}
