tasks.jar {
    enabled = true
}

tasks.bootJar {
    enabled = false
}

dependencies {
    api project(':potato-common')

    // Spring Data JPA
    api group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa', version: '2.3.8.RELEASE'

    // H2
    implementation 'com.h2database:h2'

    // MariaDB
    runtimeOnly group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: '2.6.2'

    // flyway
    implementation 'org.flywaydb:flyway-core'

    // Querydsl
    implementation "com.querydsl:querydsl-jpa"
    implementation "com.querydsl:querydsl-core"
    annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jpa"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api:2.2.3"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api:1.3.5"

    // Redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    api group: 'it.ozimov', name: 'embedded-redis', version: '0.7.2'
}

def queryDslDir = 'src/main/generated'

sourceSets {
    main.java.srcDirs += [queryDslDir]
}

tasks.withType(JavaCompile) {
    options.annotationProcessorGeneratedSourcesDirectory = file(queryDslDir)
}

clean.doLast {
    file(queryDslDir).deleteDir()
}
